// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext {
        // SDK and tools
        minSdk = 21
        compileSdk = 34
        targetSdk = 34

        // Kotlin
        kotlinVersion = "1.9.10"
        dokkaVersion = "1.9.10"

        // Example
        versionCode = properties['VERSION_CODE'].toInteger()
        versionName = "${properties['VERSION_NAME']}"

        // Libraries
        exoPlayerVersion = '1.3.1'
        analyticsVersion = '2.0.2'
    }
}
plugins {
    id 'com.android.application' version '8.5.1' apply false
    id 'com.android.library' version '8.5.1' apply false
    id 'org.jetbrains.kotlin.android' version "$kotlinVersion" apply false
    id 'org.jetbrains.dokka' version "$dokkaVersion"
}

tasks.register('clean', Delete) {
    delete rootProject.buildDir
}

tasks.dokkaHtmlMultiModule {
    moduleName.set("api.video Android $project.name library")
    suppressInheritedMembers.set(true)

    pluginsMapConfiguration.set(
            ["org.jetbrains.dokka.base.DokkaBase": """{ 
                "customAssets" : [
                    "${file("$rootDir/docs/docs_logo.svg")}",
                    "${file("$rootDir/docs/logo-icon.svg")}"
                ] 
        }"""]
    )
}