plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.dokka'
}
apply from: 'maven-push.gradle'

android {
    compileSdk 33

    defaultConfig {
        minSdk 24
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArgument "INTEGRATION_TESTS_API_KEY", "${System.getenv("INTEGRATION_TESTS_API_KEY")}"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dokkaHtml {
    moduleName.set("api.video Android $project.name library")
    suppressInheritedMembers.set(true)

    dokkaSourceSets {
        named("main") {
            noAndroidSdkLink.set(false)
            skipDeprecated.set(true)
            includeNonPublic.set(false)
            skipEmptyPackages.set(true)
        }
    }
    pluginsMapConfiguration.set(
            ["org.jetbrains.dokka.base.DokkaBase": """{ 
                "customAssets" : [
                    "${file("$rootDir/docs/docs_logo.svg")}",
                    "${file("$rootDir/docs/logo-icon.svg")}"
                ] 
        }"""]
    )
}

dependencies {

    implementation 'androidx.core:core-ktx:1.10.1'
    api 'com.android.volley:volley:1.2.1'
    implementation "video.api:android-player-analytics-exoplayer:${analyticsVersion}"
    implementation "androidx.media3:media3-ui:${exoPlayerVersion}"
    implementation "androidx.media3:media3-exoplayer:${exoPlayerVersion}"
    implementation "androidx.media3:media3-exoplayer-hls:${exoPlayerVersion}"
    implementation "androidx.media:media:1.6.0"

    testImplementation 'org.robolectric:robolectric:4.8.1'
    testImplementation 'org.robolectric:shadows-httpclient:4.5.1'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'io.mockk:mockk:1.12.2'

    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation "video.api:android-api-client:1.3.0"

}